# body for query that extracts dp axioms

#
# This file is part of ontor (https://github.com/felixocker/ontor).
# Copyright (c) 2021 Felix Ocker.
#
# ontor is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# ontor is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with ontor.  If not, see <https://www.gnu.org/licenses/>.
#

SELECT DISTINCT ?dp ?domain ?range ?minex ?minin ?maxex ?maxin ?functional ?equivalent ?parent ?disjoint WHERE 
{
    ?dp a owl:DatatypeProperty . 
    OPTIONAL { ?dp rdfs:domain ?domain . }
    OPTIONAL { 
        ?dp rdfs:range ?range . 
        FILTER NOT EXISTS { ?range owl:onDatatype ?dt . }
    }
    OPTIONAL { ?dp rdfs:range/owl:onDatatype ?range . }
    OPTIONAL { ?dp rdfs:range/owl:withRestrictions/rdf:rest*/rdf:first/xsd:minExclusive ?minex . }
    OPTIONAL { ?dp rdfs:range/owl:withRestrictions/rdf:rest*/rdf:first/xsd:minInclusive ?minin . }
    OPTIONAL { ?dp rdfs:range/owl:withRestrictions/rdf:rest*/rdf:first/xsd:maxExclusive ?maxex . }
    OPTIONAL { ?dp rdfs:range/owl:withRestrictions/rdf:rest*/rdf:first/xsd:maxInclusive ?maxin . }
    OPTIONAL { ?dp a owl:FunctionalProperty . BIND ( TRUE AS ?functional ) }
    OPTIONAL { ?dp owl:equivalentProperty ?equivalent . }
    OPTIONAL { ?dp rdfs:subPropertyOf ?parent . }
    OPTIONAL { ?dp owl:propertyDisjointWith ?disjoint . }
}
ORDER BY ?dp
