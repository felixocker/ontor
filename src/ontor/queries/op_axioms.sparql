# body for query that extracts op axioms

#
# This file is part of ontor (https://github.com/felixocker/ontor).
# Copyright (c) 2021 Felix Ocker.
#
# ontor is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# ontor is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with ontor.  If not, see <https://www.gnu.org/licenses/>.
#

SELECT DISTINCT ?op ?domain ?range ?functional ?inversefunctional ?symmetric ?asymmetric ?transitive 
?reflexive ?irreflexive ?parent ?inverseparent ?equivalent ?inverse ?disjoint ?propchain WHERE 
{
    ?op a owl:ObjectProperty . 
    OPTIONAL { ?op rdfs:domain ?domain . }
    OPTIONAL { ?op rdfs:range ?range . }
    # properties
# NOTE: more elegant, but currently unsupported expression
#    BIND ( EXISTS { ?op a owl:FunctionalProperty } AS ?functional )
    OPTIONAL { ?op a owl:FunctionalProperty .  BIND ( TRUE AS ?functional ) }
    OPTIONAL { ?op a owl:InverseFunctionalProperty .  BIND ( TRUE AS ?inversefunctional ) }
    OPTIONAL { ?op a owl:SymmetricProperty .  BIND ( TRUE AS ?symmetric ) }
    OPTIONAL { ?op a owl:AsymmetricProperty .  BIND ( TRUE AS ?asymmetric ) }
    OPTIONAL { ?op a owl:TransitiveProperty .  BIND ( TRUE AS ?transitive ) }
    OPTIONAL { ?op a owl:ReflexiveProperty .  BIND ( TRUE AS ?reflexive ) }
    OPTIONAL { ?op a owl:IrreflexiveProperty .  BIND ( TRUE AS ?irreflexive ) }
    # additional info
    OPTIONAL { 
        ?op rdfs:subPropertyOf ?parent . 
        FILTER NOT EXISTS { ?parent owl:inverseOf ?placeholder . }
    }
    OPTIONAL { ?op rdfs:subPropertyOf/owl:inverseOf ?inverseparent . }
    OPTIONAL { 
        ?op owl:equivalentProperty ?equivalent . 
        FILTER NOT EXISTS { ?equivalent owl:inverseOf ?placeholder . }
    }
    OPTIONAL { ?op owl:equivalentProperty/owl:inverseOf ?inverse . }
    OPTIONAL { ?op owl:inverseOf ?inverse . }
    OPTIONAL { ?op owl:propertyDisjointWith ?disjoint . }
    OPTIONAL { ?op owl:propertyChainAxiom/rdf:first*/rdf:rest*/rdf:first+ ?propchain . }
}
ORDER BY ?op
